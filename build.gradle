// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'signing'
apply plugin: 'maven'

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java' // where the main code is
        }
    }

    test {
        java {
            srcDir 'src/test/java' // tests for all the main code
        }
        resources {
            srcDir 'images'
        }
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile 'com.google.code.gson:gson:2.2.4'
    compile files('referencedLibraries/mnist-tools.jar')
    runtime fileTree(dir: 'referencedLibraries', include: '*.jar')

    testCompile 'junit:junit:4.11'
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

// create Gradle wrapper 
task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}


group = "com.neuronrobotics"
archivesBaseName = "WalnutiQ"
version = "2.3.2"

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar, jar
}

signing {
	sign configurations.archives
}
uploadArchives {
	repositories {
	  mavenDeployer {
		beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
  
		repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
		  authentication(userName: ossrhUsername, password: ossrhPassword)
		}
  
		snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
		  authentication(userName: ossrhUsername, password: ossrhPassword)
		}
  
		pom.project {
		  name 'WalnutiQ'
		  packaging 'jar'
		  // optionally artifactId can be defined here
		  description 'A Java based Neuron Modeling framework'
		  url 'http://neuronrobotics.com'
  
		  scm {
			connection 			'scm:git:https://github.com/NeuronRobotics/WalnutiQ'
			developerConnection 'scm:git:git@github.com:NeuronRobotics/WalnutiQ.git'
			url 'https://github.com/NeuronRobotics/WalnutiQ'
		  }
  
		  licenses {
			license {
			  name 'The Apache License, Version 2.0'
			  url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
			}
		  }
  
		  developers {
			developer {
			  id 'madhephaestus'
			  name 'Kevin Harrington'
			  email 'kharrington@neuronrobotics.com'
			}
		  }
		}
	  }
	}
  }